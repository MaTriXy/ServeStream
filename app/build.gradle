apply plugin: 'com.android.application'

android {
    compileSdkVersion 27

    flavorDimensions "default"

    def mkfile = "NDK_APPLICATION_MK=Application.mk"

    defaultConfig {
        applicationId "net.sourceforge.servestream"
        minSdkVersion 14
        targetSdkVersion 27
        versionName "0.7.3"
        versionCode 114
    }

    lintOptions {
        //checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        //abortOnError false
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets.main {
        jni.srcDirs = [] // This prevents the auto generation of Android.mk
        jniLibs.srcDir 'src/main/libs' // This is not necessary unless you have precompiled libraries in your project.
    }

    task buildFFmpeg(type: Exec, description: 'Compile FFmpeg source via NDK') {
        def dir = file('src/main/ffmpeg/build-ffmpeg.sh').absolutePath
        commandLine "$dir"
    }

    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = android.ndkDirectory
        print mkfile
        commandLine "$ndkDir/ndk-build",
                mkfile,
                '-C', file('src/main/jni').absolutePath, // Change src/main/jni the relative path to your jni source
                '-j', Runtime.runtime.availableProcessors(),
                'all',
                'NDK_DEBUG=1'
    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath, // Change src/main/jni the relative path to your jni source
                'clean'
    }

    clean.dependsOn 'cleanNative'
    buildNative.dependsOn 'buildFFmpeg'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi', 'armeabi-v7a', 'x86', 'mips'
            universalApk true
        }
    }

    productFlavors {
        regular {
            ndk {

            }
        }
        fat {
            ndk {
                abiFilters "armeabi", "armeabi-v7a", "mips", "x86"
            }
        }
        arm {
            ndk {
                abiFilter "armeabi"
            }
        }
        armv7a {
            ndk {
                abiFilter "armeabi-v7a"
            }
        }
        mips {
            ndk {
                abiFilter "mips"
            }
        }
        x86 {
            ndk {
                abiFilter "x86"
            }
        }
    }

    Map<String, Integer> versionCodes = [armeabi: 114, 'armeabi-v7a': 115, mips: 116, x86: 117]

    android.applicationVariants.all { variant ->
        // assign different version code for each output
        variant.outputs.each { output ->
            int abiVersionCode = versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI)) ?: 0
            output.versionCodeOverride = abiVersionCode + 0
            // android.defaultConfig.versionCode
        }
    }
}

dependencies {
    compile files('libs/jplaylistparser-0.2.5.jar')
    compile files('libs/jsoup-1.8.1.jar')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation project(':dslv')
    implementation 'com.android.support:design:27.1.1'
}
